configfile: "config/config.yaml"

rule all:
    input:
        "results/out.html"

rule bwa_map:
    input:
        config["datadir"] + "genome.fa",
        config["datadir"] + "samples/{sample}.fastq"
    output:
        "results/mapped_reads/{sample}.bam"
    message:
        "executing `bwa mem | samtools view` on '{input}' to generate '{output}'"
    shell:
        "bwa mem {input} | samtools view -Sb - > {output}"

rule samtools_sort:
    input:
        "results/mapped_reads/{sample}.bam"
    output:
        "results/sorted_reads/{sample}.bam"
    message:
        "executing `samtools sort` on '{input}' to generate '{output}'"
    shell:
        "samtools sort -T results/sorted_reads/{wildcards.sample} "
        "-O bam {input} > {output}"

rule samtools_index:
    input:
        "results/sorted_reads/{sample}.bam"
    output:
        "results/sorted_reads/{sample}.bam.bai"
    message:
        "executing `samtools index` on '{input}' to generate '{output}'"
    shell:
        "samtools index {input}"

rule bcftools_call:
    input:
        fa=config["datadir"] + "genome.fa",
        bam=expand("results/sorted_reads/{sample}.bam", sample=config["samples"]),
        bai=expand("results/sorted_reads/{sample}.bam.bai", sample=config["samples"])
    output:
        "results/calls/all.vcf"
    message:
        "executing `samtools mpileup | bcftools call` on '{input}' to generate '{output}'"
    shell:
        "samtools mpileup -g -f {input.fa} {input.bam} | "
        "bcftools call -mv - > {output}"

rule report:
    input:
        "results/calls/all.vcf"
    output:
        "results/out.html"
    message:
        "Generating report '{output}' from '{input}'"
    run:
        from snakemake.utils import report

        with open(input[0]) as f:
            n_calls = sum(1 for line in f if not line.startswith("#"))

        report("""
        An example workflow
        ===================================

        Reads were mapped to the Yeast reference genome 
        and variants were called jointly with
        SAMtools/BCFtools.

        This resulted in {n_calls} variants (see Table T1_).
        """, output[0], metadata="Author: Mr Pipeline", T1=input[0])
